buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0"
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
    }
}

apply plugin: "groovy"
apply plugin: "eclipse"
apply plugin: "com.gradle.plugin-publish"

defaultTasks "licenseFormat", "build"

group = "org.spongepowered"
version = buildVersion + (buildType == "RELEASE" ? "" : "-$buildType")
archivesBaseName = "mixingradle"
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    implementation gradleApi()
    implementation "com.google.guava:guava:17.0"
    implementation "org.ow2.asm:asm-debug-all:5.0.3"
    deployerJars "org.apache.maven.wagon:wagon-ftp:2.7"
}

processResources {
   from "LICENSE.txt"
}

jar.manifest.mainAttributes (
    "Built-By": System.properties["user.name"],
    "Created-By": System.properties["java.vm.version"] + " (" + System.properties["java.vm.vendor"] + ")",
    "Implementation-Title": name,
    "Implementation-Version": version
)

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    from groovydoc.destinationDir
    classifier = "groovydoc"
}

artifacts {
    archives jar
    archives groovydocJar
}

pluginBundle {
    website = "https://github.com/Skytils/MixinGradle"
    vcsUrl = "https://github.com/Skytils/MixinGradle"
    description = "Gradle plugin for SpongePowered Mixin"
    tags = ["spongepowered", "sponge", "mixin"]
    plugins {
        patcher {
            id = "org.spongepowered.mixin"
            displayName = "SpongePowered Mixin Gradle Plugin"
        }
    }
}